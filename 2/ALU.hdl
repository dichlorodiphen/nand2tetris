// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx: zero x
    Mux16(a=x, b=false, sel=zx, out=zx-x);

    // nx: negate x
    Not16(in=zx-x, out=nx-notX);
    Mux16(a=zx-x, b=nx-notX, sel=nx, out=nx-x);

    // zy: zero y
    Mux16(a=y, b=false, sel=zy, out=zy-y);

    // ny: negate y
    Not16(in=zy-y, out=ny-notY);
    Mux16(a=zy-y, b=ny-notY, sel=ny, out=ny-y);
    
    // f: x + y
    Add16(a=nx-x, b=ny-y, out=f-sum);
    And16(a=nx-x, b=ny-y, out=f-and);
    Mux16(a=f-and, b=f-sum, sel=f, out=f-result);

    // no: negate result
    Not16(in=f-result, out=no-negated);
    Mux16(a=f-result, b=no-negated, sel=no, out[0..7]=no-result-first, out[8..14]=no-result-second, out[15]=no-result-last);
    
    // zr: out == 0
    Or8Way(in=no-result-first, out=zr-nonzero-first);
    Or8Way(in[0..6]=no-result-second, in[7]=no-result-last, out=zr-nonzero-second);
    Or(a=zr-nonzero-first, b=zr-nonzero-second, out=zr-nonzero);
    Not(in=zr-nonzero, out=zr);

    // ng: out < 0
    And(a=no-result-last, b=true, out=ng);

    // out
    And16(a[0..7]=no-result-first, a[8..14]=no-result-second, a[15]=no-result-last, b=true, out=out);
}
